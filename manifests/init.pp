# @summary Simple class to manage your splunk_hec connectivity
#
# @note If you manage enable_reports, it will default to puppetdb,splunk_hec
#       If you wish to add other reports, you can do so with the reports param
#       That you can have the module automatically add the splunk_hec reports
#       processor by setting reports to '', the empty string.
#
# @example
#   include splunk_hec
#
# @param [String] url
#   The url of the server that PE is running on
# @param [String] token
#   The user token
# @param [Array] collect_facts
#   The list of facts that will be collected in the report. To collect all facts available add the special value 'all.facts'.
# @param [Boolean] enable_reports
#   Adds splunk_hec to the list of report processors
# @param [Boolean] record_event
#   If set to true, will call store_event and save report as json
# @param [Boolean] disabled
#   Disables the splunk_hec report processor
# @param [Boolean] only_changes
#   When true, only reports with a changed status with be send to Splunk
# @param [Boolean] manage_routes
#   When false, will not automatically send facts to splunk_hec
# @param [Boolean] events_reporting_enabled
#   When true, will send data from PE Event Forwarding module to Splunk
# @param [String] facts_terminus
#   Ensure that facts get saved to puppetdb
# @param [String] facts_cache_terminus
#   Makes sure that the facts get sent to splunk_hec
# @param [Optional[Array]] facts_blocklist
#   The list of facts that will not be collected in the report
# @param [Optional[String]] reports
#   Can specify report processors (other than puppetdb which is default)
#   Deprecated; should not use (will give warning).
# @param [String] pe_console
#   The FQDN for the PE console
# @param [Optional[Integer]] timeout
#   Timeout limit for for both open and read sessions
# @param [Optional[String]] ssl_ca
#   The name of the ca certification/bundle for ssl validation of the splunk_hec endpoint
# @param [Boolean] ignore_system_cert_store
#   By default, the certificate provided to the ssl_ca parameter is a supplement
#   to the system ca certificate store. If that cert store contains invalid
#   certificates, ssl validation could fail. Set this parameter to true to
#   ignore those certificates and use only the provided file.
# @param [Boolean] fips_crl_check
#   By default, the Puppet HTTP Client will attempt to check the Splunk CA against the Splunk CRL.
#   Unless the Splunk HEC endpoint is configured with a certificate generated by the Puppet CA, set
#   this parameter to false to allow metrics to successfully send.
# @param [Boolean] fips_verify_peer
#   By default, the Puppet HTTP Client will attempt peer verfication. When utilizing a self-signed
#   certificate set this parameter to false to allow metrics to successfully send.
# @param [Optional[String]] token_summary
#   Corresponds to puppet:summary in the Puppet Report Viewer
#   When storing summary in a different index than the default token
# @param [Optional[String]] token_facts
#   Corresponds to puppet:facts in the Puppet Report Viewer
#   When storing facts in a different index than the default token
# @param [Optional[String]] token_metrics
#   Corresponds to puppet:metrics in the Puppet Report Viewer
#   When storing metrics in a different index than the default token
# @param [Optional[String]] url_summary
#   Similar to token_summary; used to store summary in a different index than the default url
# @param [Optional[String]] url_facts
#   Similar to token_facts; used to store facts in a different index than the default url
# @param [Optional[String]] url_metrics
#   Similar to token_metrics; used to store metrics in a different index than the default url
# @param [Optional[Array]] include_logs_status
#   Determines if puppet logs should be included based on the return status of the puppet agent run
#   Can be none, one, or any of the following: failed, changed, unchanged
# @param [Boolean] include_logs_catalog_failure
#   Include logs if catalog fails to compile
# @param [Boolean] include_logs_corrective_change
#   Include logs if there is a corrective change
#   Only a PE feature
# @param [Optional[Array]] include_resources_status
#   Determines if resource events should be included based on return status of puppet agent run
#   Does not include 'unchanged' status reports
#   Allowed values are: failed, changed, unchanged
# @param [Boolean] include_resources_corrective_change
#   Include resource events if there is a corrective change
#   Only a PE feature
# @param [String] summary_resources_format
#   If include_resource_corrective_change or include_resources_status is set and thus resource_events
#   are being sent as part of puppet:summary events, then can choose format.
#   Allowed values are: 'hash', 'array'
# @param [Array] event_types
#   Determines which events should be forwarded to Splunk
#   Allowed values are: 'orchestrator','rbac','classifier','pe-console','code-manager'
# @param [Optional[Array]] orchestrator_data_filter
#   Filters the jobs event data
# @param [Optional[Array]] rbac_data_filter
#   Filters the rbac event data
# @param [Optional[Array]] classifier_data_filter
#   Filters the classifier event data
# @param [Optional[Array]] pe_console_data_filter
#   Filters the pe_console event data
# @param [Optional[Array]] code_manager_data_filter
#   Filters the code_manager event data
class splunk_hec (
  String $url,
  String $token,
  Array $collect_facts                                   = ['dmi','disks','partitions','processors','networking'],
  Boolean $enable_reports                                = false,
  Boolean $record_event                                  = false,
  Boolean $disabled                                      = false,
  Boolean $only_changes                                  = false,
  Boolean $manage_routes                                 = false,
  Boolean $events_reporting_enabled                      = false,
  String $facts_terminus                                 = 'puppetdb',
  String $facts_cache_terminus                           = 'splunk_hec',
  Optional[Array] $facts_blocklist                       = undef,
  Optional[String] $reports                              = undef,
  String $pe_console                                     = $settings::report_server,
  Optional[Integer] $timeout                             = undef,
  Optional[String] $ssl_ca                               = undef,
  Boolean $ignore_system_cert_store                      = false,
  Boolean $fips_crl_check                                = true,
  Boolean $fips_verify_peer                              = true,
  Optional[String] $token_summary                        = undef,
  Optional[String] $token_facts                          = undef,
  Optional[String] $token_metrics                        = undef,
  Optional[String] $url_summary                          = undef,
  Optional[String] $url_facts                            = undef,
  Optional[String] $url_metrics                          = undef,
  Optional[Array] $include_logs_status                   = undef,
  Boolean $include_logs_catalog_failure                  = false,
  Boolean $include_logs_corrective_change                = false,
  Optional[Array] $include_resources_status              = undef,
  Boolean $include_resources_corrective_change           = false,
  String $summary_resources_format                       = 'hash',
  Array $event_types                                     = ['orchestrator','rbac','classifier','pe-console','code-manager'],
  Optional[Array] $orchestrator_data_filter              = undef,
  Optional[Array] $rbac_data_filter                      = undef,
  Optional[Array] $classifier_data_filter                = undef,
  Optional[Array] $pe_console_data_filter                = undef,
  Optional[Array] $code_manager_data_filter              = undef,
) {
  # Account for the differences in Puppet Enterprise and Open Source and Agent
  $agent_node = $facts['splunk_hec_agent_only_node']

  if $agent_node {
    $owner          = 'root'
    $group          = 'root'
  }
  elsif $facts['splunk_hec_is_pe'] {
    $ini_setting    = 'pe_ini_setting'
    $ini_subsetting = 'pe_ini_subsetting'
    $service        = 'pe-puppetserver'
    $owner          = 'pe-puppet'
    $group          = 'pe-puppet'
  }
  else {
    $ini_setting    = 'ini_setting'
    $ini_subsetting = 'ini_subsetting'
    $service        = 'puppetserver'
    $owner          = 'puppet'
    $group          = 'puppet'
  }

  if $enable_reports {
    # lint:ignore:140chars
    if $reports != undef {
      notify { 'reports param deprecation warning':
        message  => 'The reports parameter has been deprecated in favor of having the module automatically add the splunk_hec setting to puppet.conf. Update the reports param to undef or remove it entirely. Please note that the reports parameter is currently ignored and will be removed in a future release of this module.',
        loglevel => 'warning',
      }
    }
    # lint:endignore
    # The subsetting resource automatically adds the 'splunk_hec' report
    # processor to the reports setting if it hasn't yet been added there.  
    Resource[$ini_subsetting] { 'enable splunk_hec':
      ensure               => present,
      path                 => '/etc/puppetlabs/puppet/puppet.conf',
      section              => 'master',
      setting              => 'reports',
      subsetting           => 'splunk_hec',
      subsetting_separator => ',',
      notify               => Service[$service],
    }
  }

  if $manage_routes {
    file { '/etc/puppetlabs/puppet/splunk_hec_routes.yaml':
      ensure  => file,
      owner   => $owner,
      group   => $group,
      mode    => '0640',
      content => epp('splunk_hec/splunk_hec_routes.yaml.epp'),
      notify  => Service[$service],
    }
    Resource[$ini_setting] { 'enable splunk_hec_routes.yaml':
      ensure  => present,
      path    => '/etc/puppetlabs/puppet/puppet.conf',
      section => 'master',
      setting => 'route_file',
      value   => '/etc/puppetlabs/puppet/splunk_hec_routes.yaml',
      require => File['/etc/puppetlabs/puppet/splunk_hec_routes.yaml'],
      notify  => Service[$service],
    }
  }

  # Decide whether to write a settings file, and if so, how to write it. A
  # settings file written to an agent node does not need to notify a service
  # to restart.
  $parameters = {
    'token' => Deferred('splunk_hec::token', [$token]),
    'url' => $url,
    'collect_facts' => $collect_facts,
    'facts_blocklist' => $facts_blocklist,
    'pe_console' => $pe_console,
    'timeout' => $timeout,
    'ssl_ca' => $ssl_ca,
    'ignore_system_cert_store' => $ignore_system_cert_store,
    'record_event' => $record_event,
    'token_summary' => $token_summary,
    'token_facts' => $token_facts,
    'token_metrics' => $token_metrics,
    'url_summary' => $url_summary,
    'url_facts' => $url_facts,
    'url_metrics' => $url_metrics,
    'include_logs_status' => $include_logs_status,
    'include_logs_catalog_failure' => $include_logs_catalog_failure,
    'include_logs_corrective_change' => $include_logs_corrective_change,
    'include_resources_status' => $include_resources_status,
    'include_resources_corrective_change' => $include_resources_corrective_change,
    'summary_resources_format' => $summary_resources_format,
    'disabled' => $disabled,
    'only_changes' => $only_changes,
    'events_reporting_enabled' => $events_reporting_enabled,
    'orchestrator_data_filter' => $orchestrator_data_filter,
    'rbac_data_filter' => $rbac_data_filter,
    'classifier_data_filter' => $classifier_data_filter,
    'pe_console_data_filter' => $pe_console_data_filter,
    'code_manager_data_filter' => $code_manager_data_filter,
    'fips_crl_check' => $fips_crl_check,
    'fips_verify_peer' => $fips_verify_peer,
    'fips_enabled' => $facts['fips_enabled'],
    'event_types' => $event_types,
  }

  if !$agent_node {
    file { "${settings::confdir}/splunk_hec.yaml":
      ensure  => file,
      owner   => $owner,
      group   => $group,
      mode    => '0640',
      content => Deferred('inline_epp', [file('splunk_hec/splunk_hec.yaml.epp'), $parameters]),
      notify  => Service[$service],
    }
  } elsif $agent_node and $events_reporting_enabled {
    file { "${settings::confdir}/splunk_hec.yaml":
      ensure  => file,
      owner   => $owner,
      group   => $group,
      mode    => '0640',
      content => Deferred('inline_epp', [file('splunk_hec/splunk_hec.yaml.epp'), $parameters]),
    }
  }

  if $events_reporting_enabled {
    if $pe_event_forwarding::confdir != undef {
      $confdir_base_path = $pe_event_forwarding::confdir
    }
    else {
      $confdir_base_path = pe_event_forwarding::base_path($settings::confdir, undef)
    }

    file { "${confdir_base_path}/pe_event_forwarding/processors.d/splunk_hec":
      ensure  => directory,
      owner   => $owner,
      group   => $group,
      require => [
        Class['pe_event_forwarding'],
        File["${settings::confdir}/splunk_hec.yaml"],
      ],
    }

    file { "${confdir_base_path}/pe_event_forwarding/processors.d/splunk_hec/util_splunk_hec.rb":
      ensure  => file,
      owner   => $owner,
      group   => $group,
      mode    => '0755',
      content => template('splunk_hec/util_splunk_hec.erb'),
      require => File["${confdir_base_path}/pe_event_forwarding/processors.d/splunk_hec"],
    }

    file { "${confdir_base_path}/pe_event_forwarding/processors.d/splunk_hec.rb":
      ensure  => file,
      owner   => $owner,
      group   => $group,
      mode    => '0755',
      source  => 'puppet:///modules/splunk_hec/splunk_hec.rb',
      require => File["${confdir_base_path}/pe_event_forwarding/processors.d/splunk_hec/util_splunk_hec.rb"],
    }
  }
}
